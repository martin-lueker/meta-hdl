#@TYPE: Machine
#@NAME: arty-lm32
#@DESCRIPTION: Arty board LM32 cpu, litex base platform

# manual arch/tune definition
DEFAULTTUNE ?= "lm32"
AVAILTUNES += "lm32"
TUNEVALID[lm32] = ""
TUNE_FEATURES_tune-lm32 = "lm32"
TUNE_ARCH = "lm32"
TUNE_PKGARCH = "lm32"
PACKAGE_EXTRA_ARCHS = "lm32"
TUNE_CCARGS .= "-mbarrel-shift-enabled -mmultiply-enabled -mdivide-enabled -msign-extend-enabled"

TCLIBC = "newlib"

# unrecognized option for gnu hash styles (unsupported)
LINKER_HASH_STYLE = ""

# litex setup
MACHINEOVERRIDES =. "litex-platform:"
MACHINEOVERRIDES =. "fpga-artix7:"
LITEX_PLATFORM = "arty"
LITEX_TARGET = "net"

# For runqemu
EXTRA_IMAGEDEPENDS += "qemu-litex-native qemu-helper-native"
QB_SYSTEM_NAME = "qemu-litex/qemu-system-lm32"
QB_DEFAULT_KERNEL = "none"
QB_MACHINE = "-machine litex"
QB_OPT_APPEND = "-nographic -serial mon:stdio"
QB_NETWORK_DEVICE = "-net nic,netdev=net0,macaddr=@MAC@"
QB_DEFAULT_FSTYPE = "none"
QB_KERNEL_CMDLINE_APPEND ?= ""

# load bios
QB_OPT_APPEND_append = " -bios ${DEPLOY_DIR_IMAGE}/bios-${MACHINE}.bin"
QB_OPT_APPEND_append = " \
    -drive if=mtd,format=qcow2,file=${DEPLOY_DIR_IMAGE}/litex-image-qemu.qcow2 \
    -global litex_ssi.spiflash=n25q128a13 \
    "
QB_SLIRP_OPT = "-netdev user,id=net0,net=192.168.100.0/24,host=192.168.100.100,dhcpstart=192.168.100.50,hostfwd=tcp::2222-:22,hostfwd=tcp::2323-:23,tftp=${DEPLOY_DIR_IMAGE}"
