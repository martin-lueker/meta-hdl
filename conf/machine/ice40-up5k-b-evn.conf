#@TYPE: Machine
#@NAME: ice40-up5k-b-evn
#@DESCRIPTION: ice40 UP5K Evaluation Board

# lm32 does not support any hash styles
LINKER_HASH_STYLE = ""
# manual arch/tune definition
DEFAULTTUNE ?= "lm32"
AVAILTUNES += "lm32"
TUNEVALID[lm32] = ""
TUNE_FEATURES_tune-lm32 = "lm32"
TUNE_ARCH = "lm32"
TUNE_PKGARCH = "lm32"
PACKAGE_EXTRA_ARCHS = "lm32"
TUNE_CCARGS .= "-mbarrel-shift-enabled -mmultiply-enabled -mdivide-enabled -msign-extend-enabled"

TCLIBC = "newlib"

# litex setup
MACHINEOVERRIDES =. "litex-platform:"
MACHINEOVERRIDES =. "fpga-ice40:"
LITEX_PLATFORM = "ice40_up5k_b_evn"
LITEX_TARGET = "base"
LITEX_CPU_VARIANT = "minimal"

# For runqemu
EXTRA_IMAGEDEPENDS += "qemu-litex-native qemu-helper-native"
QB_SYSTEM_NAME = "qemu-litex/qemu-system-lm32"
QB_DEFAULT_KERNEL = "none"
QB_MACHINE = "-machine litex"
QB_OPT_APPEND = "-nographic -serial mon:stdio"
QB_NETWORK_DEVICE = ""
QB_DEFAULT_FSTYPE = "none"
QB_KERNEL_CMDLINE_APPEND ?= ""

# setup flash, this config has no in-built bios
QB_OPT_APPEND_append = " \
    -drive if=mtd,format=qcow2,file=${DEPLOY_DIR_IMAGE}/litex-image-qemu.qcow2 \
    -global litex_ssi.spiflash=n25q032a13 \
    "
